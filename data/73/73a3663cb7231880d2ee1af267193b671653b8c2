<p>Connecting and disconnecting bluetooth devices on macOS is fairly simple: use the menu bar utility.</p>
<p><img src="https://xenodium.github.io/images/emacs-connect-my-bluetooth-speaker/macos-menu.png" alt=""></p>
<p><em>But could we make it quicker from our beloved editor?</em></p>
<p>Turns out with a little elisp glue, we can fuzzy search our Bluetooth devices and toggle connections. We can use <a href="https://twitter.com/_abo_abo">Oleh Krehel's</a> <a href="https://github.com/abo-abo/swiper">ivy-read</a> for fuzzy searching and <a href="https://twitter.com/lap_felix">Felix Lapalme</a>'s nifty <a href="https://github.com/lapfelix/BluetoothConnector">BluetoothConnector</a> to list devices and toggle Bluetooth connections.</p>
<p>As a bonus, we can make it remember the last selected device, so you can quickly toggle it again.</p>
<pre><code class="language-{.commonlisp">(defun ar/ivy-bluetooth-connect ()
  &quot;Connect to paired bluetooth device.&quot;
  (interactive)
  (assert (string-equal system-type &quot;darwin&quot;)
          nil &quot;macOS only. Sorry :/&quot;)
  (assert (executable-find &quot;BluetoothConnector&quot;)
          nil &quot;Install BluetoothConnector from https://github.com/lapfelix/BluetoothConnector&quot;)
  (ivy-read &quot;(Dis)connect: &quot;
            (seq-map
             (lambda (item)
               (let* ((device (split-string item &quot; - &quot;))
                      (mac (nth 0 device))
                      (name (nth 1 device)))
                 (propertize name
                             'mac mac)))
             (seq-filter
              (lambda (line)
                ;; Keep lines like: af-8c-3b-b1-99-af - Device name
                (string-match-p &quot;^[0-9a-f]\\{2\\}&quot; line))
              (with-current-buffer (get-buffer-create &quot;*BluetoothConnector*&quot;)
                (erase-buffer)
                ;; BluetoothConnector exits with 64 if no param is given.
                ;; Invoke with no params to get a list of devices.
                (unless (eq 64 (call-process &quot;BluetoothConnector&quot; nil (current-buffer)))
                  (error (buffer-string)))
                (split-string (buffer-string) &quot;\n&quot;))))
            :require-match t
            :preselect (when (boundp 'ar/misc-bluetooth-connect--history)
                         (nth 0 ar/misc-bluetooth-connect--history))
            :history 'ar/misc-bluetooth-connect--history
            :caller 'ar/toggle-bluetooth-connection
            :action (lambda (device)
                      (start-process &quot;BluetoothConnector&quot;
                                     (get-buffer-create &quot;*BluetoothConnector*&quot;)
                                     &quot;BluetoothConnector&quot; (get-text-property 0 'mac device) &quot;--notify&quot;))))
</code></pre>
<p><img src="https://xenodium.github.io/images/emacs-connect-my-bluetooth-speaker/connect-disconnect.gif" alt=""></p>
<p>comments on <a href="https://twitter.com/xenodium/status/1258148035319734273">twitter</a>.</p>
