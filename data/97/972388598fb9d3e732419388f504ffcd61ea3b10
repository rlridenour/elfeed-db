<p>Wanted to quickly execute an Objective-C snippet. org-babel didn't support it out of the box, but adding it was straightforward (looked at ob-C.el and ob-java.el):</p>
<pre><code class="language-{.commonlisp">(require 'ob)

(defcustom org-babel-objc-compile-command &quot;clang -x objective-c -framework Foundation&quot;
  &quot;For example: \&quot;clang -x objective-c -framework Foundation\&quot;.&quot;
  :group 'org-babel
  :version &quot;24.3&quot;
  :type 'string)

(defun org-babel-execute:objc (body params)
  &quot;Compile Objective-C BODY with org PARAMS and execute binary.&quot;
  (let* ((src-file (org-babel-temp-file &quot;org-babel-objc-block-&quot; &quot;.m&quot;))
         (cmpflag (or (cdr (assq :cmpflag params)) &quot;&quot;))
         (full-body (org-babel-expand-body:generic body params))
         (bin-file
          (org-babel-process-file-name
           (org-babel-temp-file &quot;org-babel-objc-block&quot; org-babel-exeext))))
    (with-temp-file src-file (insert full-body))
    (org-babel-eval
     (concat org-babel-objc-compile-command &quot; &quot; cmpflag &quot; &quot; src-file &quot; &quot; &quot;-o&quot; &quot; &quot; bin-file) &quot;&quot;)

    ;; Using 2&gt;&amp;1 since org babel does not include stderr in output from NSLog.
    (let ((results (org-babel-eval (concat (org-babel-process-file-name bin-file) &quot; 2&gt;&amp;1&quot;)  &quot;&quot;)))
      (org-babel-reassemble-table
       (org-babel-result-cond (cdr (assq :result-params params))
         (org-babel-read results)
         (let ((tmp-file (org-babel-temp-file &quot;c-&quot;)))
           (with-temp-file tmp-file (insert results))
           (org-babel-import-elisp-from-file tmp-file)))
       (org-babel-pick-name
        (cdr (assq :colname-names params)) (cdr (assq :colnames params)))
       (org-babel-pick-name
        (cdr (assq :rowname-names params)) (cdr (assq :rownames params)))))))

(provide 'ob-objc)
</code></pre>
<p>Add <em>objc</em> to <em>org-babel-load-languages</em>, and you can subsequently compile and run Objective-C blocks like:</p>
<pre><code class="language-{.objc">#import &lt;Foundation/Foundation.h&gt;

int main() {
  NSLog(@&quot;Hello World&quot;);
  return 0;
}
</code></pre>
