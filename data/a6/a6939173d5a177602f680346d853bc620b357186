<p>Well-ingrained into every Emacs user is the echo area, a one-stop shop to receive any kind of message from the editor, located at the bottom of the frame. Posting messages to this area from elisp couldn't be simpler:</p>
<pre><code class="language-{.commonlisp">(message &quot;Hello world&quot;)
</code></pre>
<p><img src="https://xenodium.github.io/images/spiffing-up-those-echo-messages/svg-hello-world.gif" alt=""></p>
<p>If we want to get a little fancier, we can propertize the text to add some styling.</p>
<pre><code class="language-{.commonlisp">(message (propertize &quot;hello &quot; 'face '(:foreground &quot;#C3E88D&quot;))
         (propertize &quot;world&quot; 'face '(:foreground &quot;#FF5370&quot;)))
</code></pre>
<p><img src="https://xenodium.github.io/images/spiffing-up-those-echo-messages/svg-hello-world-colors.gif" alt=""></p>
<p>With this in mind, I set out to add a tiny command to <a href="https://github.com/xenodium/ready-player">ready-player</a>.</p>
<p>I wanted the ability to ask what's on without switching to another buffer. The echo area is perfect for that. It should display track title, artist, and album.</p>
<pre><code class="language-{.commonlisp">(message (concat &quot;Ahead &quot; ;; title
                 (propertize &quot;Wire &quot; 'face '(:foreground &quot;#C3E88D&quot;)) ;; artist
                 (propertize &quot;The Ideal Copy&quot; 'face '(:foreground &quot;#FF5370&quot;)))) ;; album
</code></pre>
<p><img src="https://xenodium.github.io/images/spiffing-up-those-echo-messages/svg-ready-player-colors.gif" alt=""></p>
<p>This kinda works, but I wasn't convinced with the styling. Maybe I need multi-line?</p>
<pre><code class="language-{.commonlisp">(message (concat &quot;Ahead\n&quot; ;; title
                 (propertize &quot;Wire\n&quot; 'face '(:foreground &quot;#C3E88D&quot;)) ;; artist
                 (propertize &quot;The Ideal Copy&quot; 'face '(:foreground &quot;#FF5370&quot;)))) ;; album
</code></pre>
<p><img src="https://xenodium.github.io/images/spiffing-up-those-echo-messages/svg-ready-player-colors-multiline.gif" alt=""></p>
<p>I felt something was missing. If I could just add the album artwork as a thumbnail… The ideal layout would maybe look something like:</p>
<pre><code>+-------+
|       | Ahead
| image | Wire
|       | The Ideal Copy
+-------+
</code></pre>
<p>While the text-everywhere nature of Emacs buffers has many advantages, building more involved layouts can have its challenges. But hey, for that simple read-only message we're aiming at, we can certainly get creative without too much trouble. You see, Emacs has native svg support, so we can craft our fancy layout in elisp and tell Emacs to render it for us.</p>
<p>While I'm a noob at doing anything in svg from Emacs, adding an image and three labels, really isn't that difficult.</p>
<pre><code class="language-{.commonlisp">(message
 (let* ((image-width 90)
        (image-height 90)
        (text-height 25)
        (svg (svg-create (frame-pixel-width) image-height)))
   (svg-embed svg &quot;path/to/thumbnail.png&quot;
              &quot;image/png&quot; nil
              :x 0 :y 0 :width image-width :height image-height)
   (svg-text svg &quot;Ahead&quot;
             :x (+ image-width 10) :y text-height
             :fill (face-attribute 'default :foreground))
   (svg-text svg &quot;Wire&quot;
             :x (+ image-width 10) :y (* 2 text-height)
             :fill &quot;#C3E88D&quot;)
   (svg-text svg &quot;The Ideal Copy&quot; :x (+ image-width 10) :y (* 3 text-height)
             :fill &quot;#FF5370&quot;)
   (with-temp-buffer
     (svg-insert-image svg)
     (buffer-string))))
</code></pre>
<p>The code is fairly self-explanatory. While there may be an even simpler way (please lemme know), I used a temporary buffer to embed the svg in the propertized text prior to feeding to the handy <code>message</code> function.</p>
<p>…and with that, we get a richer display of the current track.</p>
<p><img src="https://xenodium.github.io/images/spiffing-up-those-echo-messages/svg-ready-player-colors-image.gif" alt=""></p>
<p>While I haven't experimented with other ways of creating multi-column layouts in Emacs (including images), <a href="https://indieweb.social/@xenodium">I'd love to know</a> if there's anything else available besides svg.</p>
<h2>Enjoying these tips? Using one of my Emacs packages?</h2>
<p>Help make them sustainable. Consider <a href="https://github.com/sponsors/xenodium">supporting</a> this work.</p>
