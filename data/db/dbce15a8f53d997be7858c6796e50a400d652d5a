<p>Emacs <a href="https://github.com/emacs-helm/helm">helm</a> is awesome. <a href="https://github.com/syohex/emacs-helm-ag">helm-ag</a> is double awesome. Searching for one-liners in your codebase, narrowing down with helm, and easily inserting is triple awesome.</p>
<p><img src="https://xenodium.github.io/images/search-and-insert-one-liners-with-helm-ag/helm-ag-insert.gif" alt=""></p>
<pre><code class="language-{.commonlisp">(defun ar/helm-ag (arg)
  &quot;Helm-ag search remembering last location.  With ARG, forget the last location.&quot;
  (interactive &quot;P&quot;)
  (defvar ar/helm-ag--default-locaction nil)
  (setq ar/helm-ag--default-locaction
                 (read-directory-name &quot;search in: &quot; (if arg
                                                        default-directory
                                                      ar/helm-ag--default-locaction) nil t))
  (helm-do-ag ar/helm-ag--default-locaction))

(defun ar/helm-ag-insert (arg)
  ;; Helm-ag and insert match.
  (interactive &quot;P&quot;)
  (let* ((actions (helm-make-actions
                   &quot;Insert&quot;
                   (lambda (candidate)
                     ;; Drop file:line:column. For example:
                     ;; arc_hostlink.c:13:2:#include &lt;linux/fs.h&gt;
                     ;; =&gt; #include &lt;linux/fs.h&gt;
                     (insert (replace-regexp-in-string &quot;^[^ ]*:&quot; &quot;&quot; candidate)))))
         (helm-source-do-ag (helm-build-async-source &quot;The Silver Searcher&quot;
                              :init 'helm-ag--do-ag-set-command
                              :candidates-process 'helm-ag--do-ag-candidate-process
                              :persistent-action  'helm-ag--persistent-action
                              :action actions
                              :nohighlight t
                              :requires-pattern 3
                              :candidate-number-limit 9999
                              :keymap helm-do-ag-map
                              :follow (and helm-follow-mode-persistent 1))))
    (call-interactively #'ar/helm-ag)))
</code></pre>
