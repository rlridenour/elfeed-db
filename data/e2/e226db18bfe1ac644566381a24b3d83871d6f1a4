
<p>For years now, I&#8217;ve been iterating on an AppleScript to copy a link for the currently selected Apple Mail message to the clipboard. You can then paste the link elsewhere and get a link back to the source message.</p>



<p>This came up during a recent webinar on the Apple Productivity Field Guide. Since the last time I published this script, I&#8217;ve added a few new features:</p>



<ul class="wp-block-list">
<li>Error handling plus a &#8220;Basso&#8221; sound upon error</li>



<li>Cleaner message description in the alert</li>



<li>A slight change to the way the link is created to help avoid link issues.</li>
</ul>



<p>Enjoy!</p>



<pre class="wp-block-code"><code>tell application "Mail"
    try
        set selMessages to selection
        if (count selMessages) &gt; 0 then
            -- Handle multiple message selection
            set msgCount to count selMessages
            if msgCount &gt; 1 then
                display notification (msgCount as rich text) &amp; " messages selected. Using first message." with title "Mail Link" sound name "Pop"
            end if

            set thisMsg to item 1 of selMessages
            set thisMsgID to message id of thisMsg
            set thisMsgURL to "message://%3C" &amp; thisMsgID &amp; "%3E"
            set the clipboard to thisMsgURL

            -- Truncate sender and subject for cleaner notifications
            set senderText to sender of thisMsg
            if length of senderText &gt; 30 then
                set senderText to (rich text 1 thru 30 of senderText) &amp; "..."
            end if

            set subjectText to subject of thisMsg
            if length of subjectText &gt; 40 then
                set subjectText to (rich text 1 thru 40 of subjectText) &amp; "..."
            end if

            display notification "From: " &amp; senderText &amp; " - " &amp; subjectText with title "Message Link Copied" sound name "Pop"
        else
            display notification "Please select a message first." with title "No Message Selected" sound name "Basso"
        end if
    on error errMsg
        display notification errMsg with title "Error Copying Link" sound name "Basso"
    end try
end tell</code></pre>
<p>The post <a href="https://www.macsparky.com/blog/2025/12/updated-apple-mail-link-applescript/">Updated Apple Mail Link AppleScript</a> appeared first on <a href="https://www.macsparky.com">MacSparky</a>.</p>
